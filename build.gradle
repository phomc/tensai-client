plugins {
	id "idea"
	id "eclipse"
	id "java-library"
	id "checkstyle"
	id "fabric-loom" version "1.0-SNAPSHOT"
	id "com.diffplug.spotless" version "6.12.0"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Must be added before the split source sets are setup.
	withSourcesJar()
}

dependencies {
	implementation project(':shared')
	minecraft("com.mojang:minecraft:$rootProject.minecraftVersion")
	mappings("net.fabricmc:yarn:${rootProject.minecraftVersion}+${project.yarnVersion}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loaderVersion}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

spotless {
	java {
		licenseHeaderFile(rootProject.file("HEADER"))
	}
}

checkstyle {
	toolVersion = "10.5.0"
	configFile = rootProject.file("checkstyle.xml")
}

tasks.withType(AbstractArchiveTask).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}
